group 'twino'
version '1.0'

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'nu.studer.jooq'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'nu.studer:gradle-jooq-plugin:3.0.1'
    }
}

jooq {
    version = '3.11.2'
    edition = 'OSS'
    sample(sourceSets.main) {
        jdbc {
            driver = 'org.h2.Driver'
            url = 'jdbc:h2:~/test'
            user = ''
            password = ''
        }
        generator {
            name = 'org.jooq.codegen.DefaultGenerator'
            strategy {
                name = 'org.jooq.codegen.DefaultGeneratorStrategy'
                // ...
            }
            database {
                name = 'org.jooq.meta.h2.H2Database'
                inputSchema = 'PUBLIC'
                forcedTypes {
                    forcedType {
                        name = 'varchar'
                        expression = '.*'
                        types = 'JSONB?'
                    }
                    forcedType {
                        name = 'varchar'
                        expression = '.*'
                        types = 'INET'
                    }
                }
                // ...
            }
            generate {
                relations = true
                deprecated = false
                records = true
                immutablePojos = true
                fluentSetters = true
                // ...
            }
            target {
                packageName = 'lv.twino.homework.jooq'
                 directory = 'src/main/java'
            }
        }
    }
}

run {
    main = 'lv.twino.homework.LoanManagerApplication'
}

dependencies {

    // REST client
    compile 'io.github.openfeign:feign-okhttp:9.3.1'
    compile 'io.github.openfeign:feign-gson:9.3.1'

    // A framework for building REST APIs
    compile 'com.sparkjava:spark-core:2.7.2'

    // Handles DTO serialization into JSON
    compile 'com.google.code.gson:gson:2.8.5'

    // A lightweight database
    compile 'com.h2database:h2:1.4.197'

    // A lightweight JDBC wrapper/ORM provider
    compile 'org.jooq:jooq:3.11.4'
    jooqRuntime 'com.h2database:h2:1.4.197'

    // Provides syntactic sugar by allowing to omit some of the boilerplate code
    compileOnly 'org.projectlombok:lombok:1.18.2'

    // Groovy-based testing framework
    testCompile 'org.spockframework:spock-core:1.1-groovy-2.4'
    testCompile 'cglib:cglib-nodep:3.2.0'

}
